// Enhanced functionality for use with ZDoom based ports.


// This implements a few special effects beyond mere reskinning
// and is thus presented as an optional addon to use if desired.


///////////////////////////////////////////////////////////////
// Effect 1 - Make the SSG replacment into an actual railgun //
///////////////////////////////////////////////////////////////

// Particle to be Accelerated
// This kind of weapon should not gib anything,
// hence the custom puff to override gibbing.
ACTOR Particle_Starwars : BulletPuff
{
	+NOEXTREMEDEATH
}

// Particle Accelerator
// This kind of weapon should not gib anything,
// hence the custom puff to override gibbing.
// Note also the hack of A_CustomRailgun, which
// is supposed to be used only for monsters, but
// seems to work OK on weapons. No doubt this
// will require some sort of workaround later on.
ACTOR SuperShotgun_Starwars : SuperShotgun replaces SuperShotgun
{
	Weapon.SlotNumber 3
	States
	{
	Ready:
		SHT2 K 1 A_WeaponReady
		Loop
	Deselect:
		SHT2 K 1 A_Lower
		Loop
	Select:
		SHT2 K 1 A_Raise
		Loop
	Fire:
		SHT2 K 0 A_TakeInventory ("Shell", 2)
		SHT2 K 4 Bright A_CustomRailgun (
			150, 0,
			DarkRed, DarkRed,
			RGF_NORANDOMPUFFZ,
			0, 0,
			"Particle_Starwars")
		SHT2 L 4
		SHT2 M 4
		SHT2 N 4
		SHT2 M 4
		SHT2 L 4
		Goto Ready
	Flash:
		Stop
	}
}

/////////////////////////////////////////////////////////////////////
// Effect 2 - Make the BFG spawn flaming debris around the victims //
/////////////////////////////////////////////////////////////////////

// The flames are a recreation of the effect in Duke 3D;
// however their sprites are all edited stock resources.

ACTOR BFGExtraEx_StarWars : BFGExtra_StarWars replaces BFGExtra_StarWars
{
	Damage 0
	States
	{
	Spawn:
		TNT1 A 1
		TNT1 A 1 A_CustomBulletAttack (360.0, 45.0, 16, 128, "BFGStrike_StarWars", 256.0, CBAF_AIMFACING)
		Stop
	Crash:
		TNT1 A 1
		Stop
	}
}

ACTOR BFGStrike_StarWars : BulletPuff
{
	DamageType "BFGSplash"
	RenderStyle None
	+EXTREMEDEATH
	-ALWAYSPUFF
	+PUFFONACTORS
	+BLOODLESSIMPACT
	+NODAMAGETHRUST
	+CANNOTPUSH
	States
	{
	Spawn:
		TNT1 A 1
		TNT1 A Random (4, 8)
		TNT1 A 0 A_PlaySound ("weapons/bfgflames", CHAN_6)		
		TNT1 A 0 A_SpawnItemEx ("BFGFlame_StarWars", 0, 0, 0, Random (2, 6), 0, 0, -144, SXF_NOCHECKPOSITION, 235)
		TNT1 A 0 A_SpawnItemEx ("BFGFlame_StarWars", 0, 0, 0, Random (2, 6), 0, 0,  -72, SXF_NOCHECKPOSITION, 225)
		TNT1 A 0 A_SpawnItemEx ("BFGFlame_StarWars", 0, 0, 0, Random (6, 9), 0, 0,    0, SXF_NOCHECKPOSITION, 205)
		TNT1 A 0 A_SpawnItemEx ("BFGFlame_StarWars", 0, 0, 0, Random (2, 6), 0, 0,   72, SXF_NOCHECKPOSITION, 225)
		TNT1 A 0 A_SpawnItemEx ("BFGFlame_StarWars", 0, 0, 0, Random (2, 6), 0, 0,  144, SXF_NOCHECKPOSITION, 235)
		Stop
	}
}

ACTOR BFGFlame_StarWars
{
	Radius 7
	Height 13
	Scale 0.35
	Mass 1000
	ReactionTime 6
	RenderStyle Add
	DamageType "Fire"
	Obituary "%o didn't watch where he was walking."
	States
	{
	Spawn:
		BFE3 A 4 A_Explode (1, 5, 0)
		BFE3 B 4
		BFE3 C 4
		BFE3 D 4 A_Explode (1, 5, 0)
		BFE3 E 4
		BFE3 F 4
		BFE3 G 4
		BFE3 H 4 A_Explode (1, 5, 0)
		BFE3 I 4
		BFE3 J 4
		BFE3 K 4
		BFE3 L 4 A_Explode (1, 5, 0)
		BFE3 M 4 A_Countdown
		Loop

	Death:

	Death1:
		BFE3 A 0 A_Jump (128, "Death2")
		BFE3 A 4
		BFE3 B 4
		BFE3 B 0 A_Jump (128, "Death1H")
		BFE3 C 4
		BFE3 D 4
		BFE3 E 4
		BFE3 F 4
		BFE3 G 4
	Death1H:
		BFE3 H 4 A_Explode (1, 5, 0)
		BFE3 I 4
		BFE3 J 4
		BFE3 K 4
		BFE3 L 4
		BFE3 M 4

	Death2:
		BFE3 A 0 A_Jump (128, "Death3")
		BFE3 A 4
		BFE3 B 4
		BFE3 B 0 A_Jump (128, "Death2H")
		BFE3 C 4
		BFE3 D 4
		BFE3 E 4
		BFE3 F 4
		BFE3 G 4
	Death2H:
		BFE3 H 4 A_Explode (1, 5, 0)
		BFE3 I 4
		BFE3 J 4
		BFE3 K 4
		BFE3 L 4
		BFE3 M 4

	Death3:
		BFE3 N 5 A_Explode (1, 5, 0)
		BFE3 O 5
		BFE3 P 5
		BFE3 Q 5
		Stop
	}
}
